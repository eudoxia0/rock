(in-package :cl-user)
(defpackage rock-web
  (:use :cl :cl-markup)
  (:export :index))
(in-package :rock-web)

;;; Templates

(defun head ()
  (markup
   (:head
    (:meta :charset "utf-8")
    (:meta :http-equiv "X-UA-Compatible" :content "IE=edge")
    (:meta :name "viewport" :content "width=device-width, initial-scale=1")
    (:title "Rock")
    (:link :rel "stylesheet" :href "assets/build/css/style.css"))))

(defun footer ()
  (markup
   (:footer
    (:ul
     (:li "Made by "
          (:a :href "https://github.com/eudoxia0" "eudoxia"))
     (:li :class "credit"
          "Images by "
          (:a :href "https://www.flickr.com/photos/superfamous/" "superfamous")
          ", licensed under the "
          (:a :href "http://creativecommons.org/licenses/by/3.0/"
              "CC Attribution 3.0")
          " license, via "
          (:a :href "http://thestocks.im/" "thestocks.im"))))
   (:script :src "assets/build/js/scripts.js" "")))

(defun header ()
  (markup
   (:header
    (:h1 :class "title"
         "Rock"))))

(defmacro page (&rest content)
  `(html5
    (raw (head))
    (:body
     (raw (header))
     ,@content
     (raw (footer)))))

;;; Available assets

(defmethod version-list ((asset rock::<asset>))
  (rock::versions asset))

(defmethod version-list ((asset rock::<github-asset>))
  (loop for pair in (rock::versions asset) collecting
    (first pair)))

(defun available-assets ()
  (markup
   (:div :class "list-group"
     (loop for asset-name being the hash-key of rock::*assets*
           using (hash-value asset) collecting
       (markup
        (:div :class "list-group-item"
          (:h4 :class "list-group-item-heading"
            (:code asset-name))
          (:p :class "list-group-item-text"
            "Versions: "
            (format nil "窿［夯蔺祜镳骘鲥蝮轱轭鲥蝮轱瞽扉篝狍箦舂泔祆邈糸铉蝻汶汉鲥蝮轱瞽篝蜷铉鲥蝮轱瞟┅┅┅┅换嗅珏箦翩斥礓泔溴忪镢塍邯泔溴忪镢塍斥礓邯箜狎舡聃雉弩舂ㄤ彐躅疳蝮瀛磲螂滹黝疳翳钺礤鏖翳秕麴豸麸篝蜷铉篝颟ǔ忭浜疳蝮瀛篝蜷铉犷洵痱轭舡麸篝蝈犴蹰镳候遽洵骈戾篝蜷铉疳翳钺礤篝颟┅ㄤ彐疳蜥礤翦溴筱疳蝮瀛磲螂滹黝ㄡ箐婧簌篝屙蝈灬糸鲥疳翳钺礤候镢ｐⅦ邂溴筱礓┅ㄤ彐躅轭溴ī疳珏ê磲轭蜥溴筱ê箦泗轱洪⑨篌弭螈ê璞⒘鲠殪徕戾馏箦趔蜥ㄡ鲠殪徕戾狍箦趔┅┅┅